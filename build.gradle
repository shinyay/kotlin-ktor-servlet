group 'Example'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.0'
    ext.ktor_version = '0.9.0'
    ext.kotlinx_html_version = '0.6.6'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

def withWar = project.file('web').isDirectory()
if (withWar) {
    apply plugin: 'war'

    war {
        webAppDirName = 'web'
    }
}

mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    if (withWar) {
        compile "io.ktor:ktor-server-servlet:$ktor_version"
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-jvm:$kotlinx_html_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.8.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    exclude 'it/**'
}

jar {
    //baseName 'demo'
    archiveName 'demo.jar'
    manifest {
        attributes 'Main-Class': 'io.ktor.server.netty.DevelopmentEngine'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

war {
    archiveName 'demo.war'
}

task createZip(dependsOn: 'jar', type: Zip) {
    from ('src/metadata'){include 'manifest.json'}
    from ('build/libs'){include 'demo.jar'}
    archiveName 'demo.zip'
}